!***********************************************************************
!                        Version 1:  07/2003, PFN                      *
!                                                                      *
!   QUADPRODUCT - Computes a product quadrature set based on the       *
!                 number of polar and azimuthal directions and the     *
!                 specified polar axis.                                *
!                                                                      *
!   Input:   nang       - number of angles                             *
!            npolar     - number of polar angles                       *
!            nazimuthal - number of azimuthal angles                   *
!            polaraxis  - polar axis                                   *
!                                                                      *
!   Output:  OMEGA  - group dependent direction cosines (mu,eta,xi)    *
!            QUADWT - group dependent quadrature weights               *
!                                                                      *
!   Allowed values:  1 <= npolar <= 7, 1 <= nazimuthal <= 7            *
!                                                                      *
!                                                                      *
!***********************************************************************

   subroutine quadProduct(self)

   use kind_mod
   use constant_mod
   use Quadrature_mod
   use QuadratureData_mod

   implicit none

!  Arguments

   type(Quadrature)          :: self

!  Local

   integer    :: i,iPhi,jTheta,m,Phi1,Phi2,Theta1,Theta2,nangoct

   integer    :: npolar,nazimuthal,polaraxis

   real(adqt) :: cosTheta

   real(adqt) :: omegaX(self% npolar*self% nazimuthal),  &
                 omegaY(self% npolar*self% nazimuthal),  &
                 omegaZ(self% npolar*self% nazimuthal),  &
                 qweight(self% npolar*self% nazimuthal)

!  Constants

   npolar     = self% npolar
   nazimuthal = self% nazimuthal
   polaraxis  = self% polaraxis

   Phi1    = first(nazimuthal) 
   Phi2    =  last(nazimuthal) 
   Theta1  = first(npolar) 
   Theta2  =  last(npolar) 

   nangoct = npolar*nazimuthal

!  Set omegaX, omegaY, omegaZ based on the choice of polar axis

   m = 0
   do iPhi=Phi1,Phi2
     do jTheta=Theta1,Theta2

       cosTheta = sqrt( one - sineTheta(jTheta)*sineTheta(jTheta) )
       m = m + 1

       if (polaraxis == 1) then
         omegaX(m) = cosTheta
         omegaZ(m) = sineTheta(jTheta)*sinePhi(iPhi)
         omegaY(m) = sqrt( one - omegaZ(m)*omegaZ(m) - omegaX(m)*omegaX(m) )
       elseif (polaraxis == 2) then
         omegaY(m) = cosTheta
         omegaX(m) = sineTheta(jTheta)*sinePhi(iPhi)
         omegaZ(m) = sqrt( one - omegaX(m)*omegaX(m) - omegaY(m)*omegaY(m) )
       elseif (polaraxis == 3) then
         omegaZ(m) = cosTheta
         omegaY(m) = sineTheta(jTheta)*sinePhi(iPhi)
         omegaX(m) = sqrt( one - omegaZ(m)*omegaZ(m) - omegaY(m)*omegaY(m) )
       endif

       qweight(m) = wgtTheta(jTheta)*wgtPhi(iPhi)

     enddo
   enddo

!  Set the direction cosines and weights; note that the
!  angles are numbered consecutively in an octant.
!  NANGOCT is the number of angles per octant.


   do i=1,nangoct

!  Octant 1  mu>0, eta>0, xsi>0

     self% omega(1,i) = omegaX(i) 
     self% omega(2,i) = omegaY(i) 
     self% omega(3,i) = omegaZ(i) 
     self% weight(i)  = qweight(i) 

!  Octant 2  mu<0, eta>0, xsi>0

     self% omega(1,nangoct+i)   = -omegaX(i)
     self% omega(2,nangoct+i)   =  omegaY(i) 
     self% omega(3,nangoct+i)   =  omegaZ(i) 
     self% weight(nangoct+i)    =  qweight(i) 

!  Octant 3  mu<0, eta<0, xsi>0

     self% omega(1,2*nangoct+i) = -omegaX(i)
     self% omega(2,2*nangoct+i) = -omegaY(i)
     self% omega(3,2*nangoct+i) =  omegaZ(i) 
     self% weight(2*nangoct+i)  =  qweight(i) 

!  Octant 4  mu>0, eta<0, xsi>0

     self% omega(1,3*nangoct+i) =  omegaX(i) 
     self% omega(2,3*nangoct+i) = -omegaY(i)
     self% omega(3,3*nangoct+i) =  omegaZ(i) 
     self% weight(3*nangoct+i)  =  qweight(i) 

!  Octant 5  mu>0, eta>0, xsi<0

     self% omega(1,4*nangoct+i) =  omegaX(i) 
     self% omega(2,4*nangoct+i) =  omegaY(i) 
     self% omega(3,4*nangoct+i) = -omegaZ(i)
     self% weight(4*nangoct+i)  =  qweight(i) 

!  Octant 6  mu<0, eta>0, xsi<0

     self% omega(1,5*nangoct+i) = -omegaX(i)
     self% omega(2,5*nangoct+i) =  omegaY(i) 
     self% omega(3,5*nangoct+i) = -omegaZ(i)
     self% weight(5*nangoct+i)  =  qweight(i) 

!  Octant 7  mu<0, eta<0, xsi<0

     self% omega(1,6*nangoct+i) = -omegaX(i)
     self% omega(2,6*nangoct+i) = -omegaY(i)
     self% omega(3,6*nangoct+i) = -omegaZ(i)
     self% weight(6*nangoct+i)  =  qweight(i) 

!  Octant 8  mu>0, eta<0, xsi<0

     self% omega(1,7*nangoct+i) =  omegaX(i) 
     self% omega(2,7*nangoct+i) = -omegaY(i)
     self% omega(3,7*nangoct+i) = -omegaZ(i)
     self% weight(7*nangoct+i)  =  qweight(i) 

   enddo


   return
   end subroutine quadProduct


